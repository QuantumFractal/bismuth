start: command+

command: a_move   | r_move
       | a_line   | r_line
       | a_hline  | r_hline
       | a_vline  | r_vline
       | a_curve  | r_curve
       | a_scurve | r_scurve
       | a_qcurve | r_qcurve
       | a_tcurve | r_tcurve
       | close_path

// M x y (or m dx dy)
a_move: "M" WS* pair WS*
r_move: "m" WS* pair WS*

// L x y (or l dx dy)
a_line: "L" WS* pair WS*
r_line: "l" WS* pair WS*

// H x (or h dx)
a_hline: "H" WS* NUMBER WS*
r_hline: "h" WS* NUMBER WS*

// V y (or v dy)
a_vline: "V" WS* NUMBER WS*
r_vline: "v" WS* NUMBER WS*

// C x1 y1, x2 y2, x y (or c dx1 dy1, dx2 dy2, dx dy)
a_curve: "C" WS* pair _sep pair _sep pair WS*
r_curve: "c" WS* pair _sep pair _sep pair WS*

// S x2 y2, x y (or s dx2 dy2, dx dy)
a_scurve: "S" WS* pair _sep pair WS*
r_scurve: "s" WS* pair _sep pair WS*

// Q x1 y1, x y (or q dx1 dy1, dx dy)
a_qcurve: "Q" WS* pair _sep pair WS*
r_qcurve: "q" WS* pair _sep pair WS*

//  T x y (or t dx dy)
a_tcurve: "T" WS* pair WS*
r_tcurve: "t" WS* pair WS*

// Z z z z z z z z
close_path: "Z" | "z"

pair: NUMBER _sep NUMBER
_sep: "," WS* | WS+

%import common.NUMBER
%import common.WS